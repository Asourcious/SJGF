/*
 *     Copyright 2017 Patrick Barron
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import org.gradle.internal.os.OperatingSystem

plugins {
    id "java-library"
    id "idea"
    id "com.zyxist.chainsaw" version "0.1.3"
}

group "org.barronpm"
version "0.1-alpha"
javaModule.name = "org.barronpm.sjgf"
javaModule.allowModuleNamingViolations = true
sourceCompatibility = 1.9

ext {
    versions = [
            slf4j:"1.7.25",
            logback:"1.2.3",
            lwjgl:"3.1.3"
    ]

    switch ( OperatingSystem.current() ) {
        case OperatingSystem.WINDOWS:
            lwjglNatives = "natives-windows"
            break
        case OperatingSystem.LINUX:
            lwjglNatives = "natives-linux"
            break
        case OperatingSystem.MAC_OS:
            lwjglNatives = "natives-macos"
            break
    }
}

repositories {
    mavenCentral()
}

dependencies {
    api "org.slf4j:slf4j-api:${versions.slf4j}"

    implementation "ch.qos.logback:logback-core:${versions.logback}"
    implementation "ch.qos.logback:logback-classic:${versions.logback}"

    implementation "org.lwjgl:lwjgl:${versions.lwjgl}"
    implementation "org.lwjgl:lwjgl-glfw:${versions.lwjgl}"
    implementation "org.lwjgl:lwjgl-jemalloc:${versions.lwjgl}"
    implementation "org.lwjgl:lwjgl-openal:${versions.lwjgl}"
    implementation "org.lwjgl:lwjgl-opengl:${versions.lwjgl}"

    runtimeOnly "org.lwjgl:lwjgl:${versions.lwjgl}:$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-glfw:${versions.lwjgl}:$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-jemalloc:${versions.lwjgl}:$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-openal:${versions.lwjgl}:$lwjglNatives"
    runtimeOnly "org.lwjgl:lwjgl-opengl:${versions.lwjgl}:$lwjglNatives"
}

jar {
    baseName = project.name
    manifest {
        attributes 'Implementation-Version': version
    }
}

